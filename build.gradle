buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.netflix.nebula:gradle-ospackage-plugin:5.3.0'
        classpath 'org.spockframework:spock-core:1.2-groovy-2.5'
        classpath 'edu.sc.seis.gradle:launch4j:2.4.4'
    }
}

apply plugin: 'java'
apply plugin: 'distribution'
apply plugin: 'edu.sc.seis.launch4j'
apply plugin: 'nebula.ospackage'


// Common configuration //
version='1.4.2'
targetCompatibility = '1.7'

allprojects {
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    tasks.withType(JavaCompile) {
        sourceCompatibility = targetCompatibility = '1.7'
        options.compilerArgs << '-Xlint:unchecked'
    }
    tasks.withType(GroovyCompile) {
        sourceCompatibility = targetCompatibility = '1.7'
        options.compilerArgs << '-Xlint:unchecked'
    }

    repositories {
        mavenCentral()
        maven {
            url 'https://raw.github.com/java-decompiler/mvn-repo/master'
        }
    }

    configurations {
        provided
        compile.extendsFrom provided
    }
}

// 'cleanIdea' task extension //
cleanIdea {
    file(project.name + '.iws').delete()
    ant.delete(dir: 'out')
}

// All in one JAR //
subprojects.each { subproject ->
    evaluationDependsOn(subproject.path)
}

jar {
    dependsOn subprojects.tasks['jar']

    // Add SPI directory
    def tmpSpiDir = file('build/tmp/spi')
    tmpSpiDir.deleteDir()
    def tmpSpiServicesDir = file(tmpSpiDir.path + '/META-INF/services')
    tmpSpiServicesDir.mkdirs()
    from tmpSpiDir

    // Add dependencies
    def deps = []
    subprojects.each { subproject ->
        from subproject.sourceSets.main.output.classesDirs
        from subproject.sourceSets.main.output.resourcesDir
        deps += subproject.configurations.runtime - subproject.configurations.provided
    }
    subprojects.each { subproject ->
        deps -= subproject.jar.archivePath
    }
    deps = deps.unique().collect { it.isDirectory() ? it : zipTree(it) }
    from deps

    manifest {
        attributes 'Main-Class': 'org.jd.gui.App',
                'SplashScreen-Image': 'org/jd/gui/images/jd_icon_128.png',
                'JD-GUI-Version': project.version,
                'JD-Core-Version': '0.7.1'
    }
    exclude 'META-INF/groovy/**', 'META-INF/licenses/**', 'META-INF/maven/**', 'META-INF/INDEX.LIST'
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
    doFirst {
        // Copy and merge SPI config files
        subprojects.each { subproject ->
            def servicesDir = file(subproject.sourceSets.main.output.resourcesDir.path + '/META-INF/services')
            if (servicesDir.exists()) {
                servicesDir.eachFile { source ->
                    def target = file(tmpSpiServicesDir.path + '/' + source.name)
                    target << source.text
                }
            }
        }
    }
}

// Java executable wrapper for Windows //
launch4j {
    version = textVersion = project.version
    fileDescription = productName = 'JD-GUI'
    errTitle 'JD-GUI Windows Wrapper'
    copyright 'JD-GUI (C) 2008-2019 Emmanuel Dupuy'
    icon projectDir.path + '/src/launch4j/resources/images/jd-gui.ico'
}

// Packages for Linux //
ospackage {
    license = file('LICENSE')
    maintainer 'Emmanuel Dupuy <emmanue1@users.noreply.github.com>'
    os LINUX
    packageDescription 'JD-GUI, a standalone graphical utility that displays Java sources from CLASS files'
    packageGroup 'java'
    packageName project.name
    release '0'
    summary 'A Java Decompiler'
    url 'https://github.com/java-decompiler/jd-gui'

    into '/opt/' + project.name
    from ('build/libs/') {
        fileMode 0755
    }
    from ('src/linux/resources/') {
        fileMode 0755
    }
    from 'LICENSE', 'NOTICE', 'README.md'

    postInstall 'cd /opt/'+ project.name+'; ln -s ./jd-gui-'+project.version+'.jar ./jd-gui.jar; xdg-icon-resource install --size 128 --novendor ./jd_icon_128.png jd-gui; xdg-desktop-menu install ./*.desktop'
    preUninstall 'cd /opt/'+ project.name+'; rm -f ./jd-gui.jar; rm -fr ./ext; xdg-desktop-menu uninstall ./*.desktop'
}

// Distributions for OSX and Windows //
distributions {
    osx.contents {
        into('JD-GUI.app/Contents') {
            from('src/osx/resources') {
                include 'Info.plist'
                expand VERSION: project.version
            }
        }
        into('JD-GUI.app/Contents/MacOS') {
            from('src/osx/resources') {
                include 'universalJavaApplicationStub.sh'
                fileMode 0755
            }
        }
        into('JD-GUI.app/Contents/Resources/Java') {
            from jar.archivePath
        }
        from 'LICENSE', 'NOTICE', 'README.md'
    }
    windows.contents {
        from 'build/launch4j/jd-gui.exe'
        from 'LICENSE', 'NOTICE', 'README.md'
    }
}

installWindowsDist.dependsOn createExe
windowsDistTar.dependsOn createExe
windowsDistZip.dependsOn createExe

buildDeb.dependsOn jar
buildRpm.dependsOn jar

build.finalizedBy buildDeb
build.finalizedBy buildRpm
